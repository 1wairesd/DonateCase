import proguard.gradle.ProGuardTask

buildscript {
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath 'com.guardsquare:proguard-gradle:7.4.1'
    }
}
plugins {
    id 'java'
    id 'com.github.johnrengelman.shadow' version '7.1.2'
    id 'com.rikonardo.papermake' version '1.0.6'
}

group = 'com.jodexindustries.donatecase'
version = '2.1.7.1'

repositories {
    mavenCentral()
    maven {
        name = "placeholderapi"
        url = "https://repo.extendedclip.com/content/repositories/placeholderapi"
    }
    maven {
        name = "jitpack.io"
        url = "https://jitpack.io"
    }
    maven {
        url = 'https://hub.spigotmc.org/nexus/content/repositories/snapshots/'
        content {
            includeGroup 'org.bukkit'
            includeGroup 'org.spigotmc'
        }
    }
    maven { url "https://repo.dmulloy2.net/repository/public/" }
    maven {
        name = 'authlib'
        url = 'https://papermc.io/repo/repository/maven-releases/'
    }
    maven {
        name = "jodexindustries"
        url = "https://repo.jodexindustries.space/releases"
    }
    maven {
        name = "sonatype"
        url = "https://oss.sonatype.org/content/groups/public/"
    }
    maven  { url 'https://repo.alessiodp.com/releases/' }
    maven  { url 'https://repo.mineinabyss.com/releases' }
    maven { url 'https://repo.lukasa.lt/repository/maven-public/' }


}

dependencies {
    compileOnly 'org.jetbrains:annotations:24.0.0'
    compileOnly 'org.spigotmc:spigot-api:1.20.4-R0.1-SNAPSHOT'
    compileOnly "com.github.MilkBowl:VaultAPI:1.7"
    compileOnly "me.clip:placeholderapi:2.11.2"
    compileOnly "com.arcaniax:HeadDatabase-API:1.3.1"
    compileOnly "de.likewhat.customheads:CustomHeads:3.0.7"
    compileOnly 'com.github.LoneDev6:API-ItemsAdder:3.6.1'
    compileOnly 'com.mojang:authlib:1.5.25'

}

def dependsDir = "${buildDir}/libs/dependencies/"

configurations.implementation.setCanBeResolved(true)

tasks.register('copyDepends', Copy) {
    from configurations.implementation
    into "${dependsDir}"
}

tasks.register('proguard', ProGuardTask) {
    dependsOn tasks.shadowJar
    configuration file('proguard.conf')
    injars tasks.shadowJar.outputs.files
    libraryjars files("D:/Java/Java11/jmods/")
    libraryjars files(dependsDir)
    verbose
    outjars "${buildDir}/libs/${project.name}-${version}.jar"
}



build.dependsOn proguard

def targetJavaVersion = 11
java {
    def javaVersion = JavaVersion.toVersion(targetJavaVersion)
    sourceCompatibility = javaVersion
    targetCompatibility = javaVersion
    if (JavaVersion.current() < javaVersion) {
        toolchain.languageVersion = JavaLanguageVersion.of(targetJavaVersion)
    }
}

tasks.withType(JavaCompile).configureEach {
    if (targetJavaVersion >= 10 || JavaVersion.current().isJava10Compatible()) {
        options.release = targetJavaVersion
    }
}

processResources {
    def props = [version: version]
    inputs.properties props
    filteringCharset 'UTF-8'
    filesMatching('plugin.yml') {
        expand props
    }
}
tasks.shadowJar {
    archiveBaseName.set("shadow")
    archiveClassifier.set('')
    archiveVersion.set('')
}